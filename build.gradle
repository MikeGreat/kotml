buildscript {
    ext.kotlin_version = '1.2.31'
    ext.dokka_version = '0.9.16'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

//plugins {
//    id "com.jfrog.bintray" version "1.7.3"
//}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven-publish'
apply from: 'keystore.gradle'
apply plugin: 'com.jfrog.bintray'

version '0.1-ALPHA'
group 'com.tommypacker'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "http://dl.bintray.com/kyonifer/maven"
        jcenter()
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "de.mpicbg.scicomp:krangl:0.9"
    implementation group: "koma", name:"core", version:"0.11"
    implementation group: "koma", name:"backend-matrix-ejml", version: "0.11"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

bintray {
    user = 'tommypacker'
    key = bintray_key
    publish = true
    override = true
    publications = ['maven']
    pkg {
        repo = 'kotml'
        name = 'kotml'

        licenses = ['MIT']
        vcsUrl = 'https://github.com/tommypacker/kotml'

        version {
            name = project.version
            released = new Date()
            vcsTag = "v${project.version}"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: project.classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: project.javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "tommypacker"
            name "Thomas Yu"
            email "tommypacker@gmail.com"
        }
    }

    scm {
        url "https://github.com/tommypacker/kotml"
    }
}


publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
            version = project.version
            artifactId = 'kotml'
            groupId = project.group
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Machine Learning library written in Kotlin')
                root.appendNode('name', 'kotml')
                root.appendNode('url', 'https://github.com/tommypacker/kotml')
                root.children().last() + pomConfig
            }
        }
    }
}